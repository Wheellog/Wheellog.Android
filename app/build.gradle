apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    mavenCentral()
    google()
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file('../debug.keystore')
        }
    }

    defaultConfig {
        applicationId "com.cooper.wheellog"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 97
        versionName "2.5.1"
        buildConfigField 'String', 'BUILD_TIME', "\"" + new Date().format('HH:mm dd.MM.yyyy') + "\""
        buildConfigField 'String', 'BUILD_DATE', "\"" + new Date().format('dd.MM.yyyy') + "\""
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
    }
    buildTypes.all { buildType ->
        // private properties
        def props = new Properties()
        def propsPath =  "../local.properties"
        if (file(propsPath).exists()) {
            file(propsPath).withInputStream {
                stream -> props.load(stream)
            }
        }
        def ec_token = System.getProperty("ec_accessToken") ?: props.getProperty('ec_accessToken') ?: ""
        buildType.buildConfigField 'String', 'ec_accessToken', "\"" + ec_token + "\""
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE*'
        exclude 'META-INF/NOTICE*'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/connectiq.jar')
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.getpebble:pebblekit:4.0.1'
    implementation 'me.relex:circleindicator:2.1.6'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.github.permissions-dispatcher:permissionsdispatcher:4.8.0'
    implementation 'org.osmdroid:osmdroid-android:6.1.11'
    implementation project(path: ':shared')
    annotationProcessor 'com.github.permissions-dispatcher:permissionsdispatcher-processor:4.8.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.google.guava:guava:30.1.1-jre'
    // coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation 'io.mockk:mockk:1.12.0'
    testImplementation 'org.robolectric:robolectric:4.4'
    testImplementation 'androidx.test:core:1.4.0'
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    implementation 'com.google.android.gms:play-services-wearable:17.1.0'
    wearApp project(':wearos')
}
