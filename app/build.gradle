apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        applicationId "com.cooper.wheellog"
        minSdkVersion 18
        targetSdkVersion 29
        versionCode 75
        versionName "2.4.1M"
        buildConfigField 'String', 'BUILD_TIME', 'new java.text.SimpleDateFormat("HH:mm dd.MM.yyyy", java.util.Locale.US).format(new java.util.Date(' + System.currentTimeMillis() +'L))'
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
        }
        // private properties
        def props = new Properties()
        def propsPath =  "../local.properties"
        if (file(propsPath).exists()) {
            file(propsPath).withInputStream {
                stream -> props.load(stream)
            }
        }
        buildTypes.each {
            it.buildConfigField 'String', 'ec_accessToken', props.getProperty('ec_accessToken') ?: "\"\""
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE*'
        exclude 'META-INF/NOTICE*'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation files('libs/connectiq.jar')
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.getpebble:pebblekit:4.0.1'
    implementation 'com.github.JakeWharton:ViewPagerIndicator:2.4.1'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.0'
    implementation 'com.github.hotchemi:permissionsdispatcher:2.2.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.google.guava:guava:29.0-android'

    testImplementation 'junit:junit:4.13'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.0'
    testImplementation 'io.mockk:mockk:1.10.2'

    annotationProcessor "com.github.hotchemi:permissionsdispatcher-processor:2.2.0"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
}
